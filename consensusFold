#!/usr/bin/env perl

use strict;
use File::Path qw(mkpath);
use Getopt::Long qw(:config no_ignore_case);

use lib "/path/to/RNAFramework/lib";

use Core::Mathematics qw(:all);
use Core::Process::Queue;
use Core::Utils;
use Data::IO;
use Graphics::Container;
use Graphics::Object::Barplot;
use Graphics::Object::Path;
use Graphics::Object::RNAarcs;
use Graphics::Object::Ruler;
use Interface::ViennaRNA;
use RF::Data::IO::XML;
use Term::Progress;

$|++;

my ($slope, $intercept, $output, $overwrite, 
    $help, $error, $progressBar, $queue,
    $processors, $maxBPdist, $temperature, $genPlots,
    @dirs, @files);

do {

    local $SIG{__WARN__} = sub { };

    GetOptions( "h|help"          => \$help,
                "sl|slope=s"      => \$slope,
                "in|intercept=s"  => \$intercept,
                "md|maxBPdist=s"  => \$maxBPdist,
                "t|temperature=s" => \$temperature,
                "o|output=s"      => \$output,
                "ow|overwrite"    => \$overwrite,
                "p|processors=s"  => \$processors,
                "g|plot"          => \$genPlots ) or help(1);

    @dirs = @ARGV;

};

help() if ($help);

$processors ||= 1;
$slope ||= 1.8;
$intercept ||= -0.6;
$temperature //= 37;
$output ||= "consensusFold_out/";
$output =~ s/\/?$/\//;

die "\n  [!] Error: Number of processors must be a positive INT > 0\n\n" if (!isnumeric($processors) || !$processors);
die "\n  [!] Error: At least 2 XML input directories needed\n\n" unless (@dirs);
for (@dirs) { die "\n  [!] Error: Path to XML input directory \"" . $_ . "\" does not point to a folder\n\n" unless (-d $_); }
die "\n  [!] Error: Slope must be numeric\n\n" if (!isnumeric($slope));
die "\n  [!] Error: Intercept must be numeric\n\n" if (!isnumeric($intercept));
die "\n  [!] Error: Max base-pair distance must be a positive INT > 3\n\n" if (defined $maxBPdist && (!isint($maxBPdist) || $maxBPdist < 4));
die "\n  [!] Error: Temperature must be <= 100\n\n" if (!isnumeric($temperature) || $temperature > 100);

print "\n[+] Making output directory...";

if (-e $output) {

    if ($overwrite) {

        $error = rmtree($output);

        die "\n\n  [!] Error: " . $error . "\n\n" if ($error);

    }
    else { die "\n\n  [!] Error: Output directory already exists." .
               "\n      Please use -ow (or --overwrite) to overwrite output directory\n\n"; }

}

mkpath($output . "structures", { mode  => 0755,
                                 error => \$error });
mkpath($output . "images", { mode  => 0755,
                             error => \$error }) if ($genPlots);

die "\n\n  [!] Error: Unable to create output directory (" . $error->[0]->{each(%{$error->[0]})} . ")\n\n" if (@{$error});

opendir(my $dh, $dirs[0]) or die "\n\n  [!] Error: Unable to list directory \"" . $dirs[0] . "\" content\n\n";
FILES:
while (my $file = readdir($dh)) {

    next if (substr($file, -4) ne ".xml");

    for (@dirs) { next FILES if (!-e $_ . "/" . $file); }

    push(@files, $file);

}
closedir($dh);

print "\n[+] Folding transcripts. Please wait...\n\n";

$progressBar = Term::Progress->new( max   => scalar(@files),
                                    width => 50 );
$queue = Core::Process::Queue->new( processors   => $processors,
                                    parentOnExit => sub { $progressBar->update(1); },
                                    stderr       => $output . "errors.out",
                                    verbosity    => -1 );
$progressBar->init("");
$queue->enqueue( command   => \&fold,
                 arguments => [ $_ ],
                 id        => $_ ) for (@files);
$queue->start();
$queue->waitall();

print "\n\n[+] All done.\n\n";

sub fold {

    my $file = shift;

    my ($intf, $id, $struct, %reacts, 
        %seq);

    foreach my $dir (@dirs) {

        my $xml = RF::Data::IO::XML->new(file => $dir . "/" . $file);
        $id = $xml->id();
        $dir =~ s/\///g;
        $seq{$dir} = $xml->sequence();
        $reacts{$dir} = [ $xml->reactivity() ];

    }

    $intf = Interface::ViennaRNA->new();
    $struct = $intf->alifold( \%seq,
                              { slope             => $slope,
                                intercept         => $intercept,
                                maxBPdist         => $maxBPdist,
                                reactivity        => \%reacts,
                                temperature       => $temperature,
                                partitionFunction => $genPlots ? 1 : 0 });

    $file =~ s/\.xml$/\.db/;

    open(my $fh, ">", $output . "/structures/" . $file) or die "[!] Failed to write to \"" . $output . "/" . $file . "\" (PID: $$, $!)\n";
    print $fh ">" . $id . "\n" . $struct->sequence() . "\n" . $struct->structure() . "\n";
    close($fh);

    if ($genPlots) {

        $file =~ s/\.db$/.svg/;

        my ($ticksBy, $image, $io, $reactivity, 
            $structure, $ruler, $pairProb, @reacts, 
            @averageReact, @dotplot, %bpProb);
        $ticksBy = max(1, round($struct->length() / 10));
        $image = Graphics::Container->new( height  => abs(maprange(0, 1000, 1000, 500, min($struct->length(), 1000))), 
                                           width   => 1000,
                                           spacing => 0.02 );
        $io = Data::IO->new( file    => $output . "/images/" . $file,
                             mode    => "w",
                             binmode => ":encoding(utf-8)" );
        @reacts = map { my $i = $_; [ map { $reacts{$_}->[$i] } keys %reacts ] } 0 .. $struct->length() - 1;
        @averageReact = map { isnumeric(@$_) ? min(mean(@$_), 2) : "NaN" } @reacts;
        %bpProb = $struct->bpprobability();

        foreach my $i (sort {$a <=> $b} keys %bpProb) {

            foreach my $j (sort {$a <=> $b} keys %{$bpProb{$i}}) {

                next if ($i > $j);

                push(@dotplot, [$i, $j, $bpProb{$i}->{$j}]) if ($bpProb{$i}->{$j} >= 0.05);

            }

        }

        $reactivity = Graphics::Object::Barplot->new( fontsize  => abs(maprange(0, 1000, -30, -10, min($struct->length(), 1000))),
                                                      height    => 1,
                                                      values    => \@averageReact,
                                                      stdev     => [ map { isnumeric(@$_) ? stdev(@$_) : "NaN" } @reacts ],
                                                      plotstdev => "up",
                                                      yname     => "Reactivity",
                                                      barfill   => [ map { $_ <= 0.4 ? "black" : ($_ < 0.7 ? "rgb(255,204,0)" : "rgb(153,25,23)") } @averageReact ],
                                                      barstroke => [ map { $_ <= 0.4 ? "black" : ($_ < 0.7 ? "rgb(255,204,0)" : "rgb(153,25,23)") } @averageReact ] );
        $structure = Graphics::Object::RNAarcs->new( height    => 2,
                                                     length    => $struct->length(),
                                                     flip      => "down",
                                                     basepairs => [ $struct->basepairs() ] );
        $ruler = Graphics::Object::Ruler->new( fontsize => abs(maprange(0, 1000, -30, -10, min($struct->length(), 1000))),
                                               height   => 1,
                                               range    => [ 0, $struct->length() ],
                                               ticksby  => $ticksBy,
                                               name     => "Position (nt)",
                                               toend    => 1 );
        $pairProb = Graphics::Object::RNAarcs->new( height    => 2,
                                                    length    => $struct->length(),
                                                    flip      => "down",
                                                    basepairs => [ map { [$_->[0], $_->[1]] } @dotplot ],
                                                    stroke    => [ map { $_->[2] < 0.1 ? "rgb(150,150,150)" : ($_->[2] < 0.4 ? "rgb(255,204,0)" : ($_->[2] < 0.7 ? "rgb(71,142,204)" : "rgb(81,183,71)")) } @dotplot ] );
    
        $image->addobjects($reactivity, $structure, $pairProb, 0.05, $ruler);
        $io->write($image->xml());
        $io->close();
    
    }

}

sub help {

    print "\n  [!] Error: Invalid option. Please check the help\n" if ($_[0]);

    die <<HELP;

 consensusFold (using RNA Framework v$Core::Utils::VERSION)

 Author:  Danny Incarnato (dincarnato[at]rnaframework.com)
 Summary: Prediction of consensus structure from multiple reactivity profiles

 Usage:   consensusFold [Options] XML_dir_1/ XML_dir_2/ ... XML_dir_n/

 Options                               Description
 -o  or --output          <string>     Output directory (Default: consensusFold_out/)
 -ow or --overwrite                    Overwrites output directory (if the specified directory already exists)
 -p  or --processors      <int>        Number of processors to use (>=1, Default: 1)
 -sl or --slope           <float>      Sets the slope used with structural probing data restraints (Default: 1.8 [kcal/mol])
 -in or --intercept       <float>      Sets the intercept used with structural probing data restraints (Default: -0.6 [kcal/mol])
 -md or --maxBPdist       <int>        Maximum allowed base-pair distance (>3, Default: no limit)
 -t  or --temperature     <float>      Folding temperature, in Celsius degrees (<=100, Default: 37)
 -g  or --plot                         Generate structure plots

HELP

}
